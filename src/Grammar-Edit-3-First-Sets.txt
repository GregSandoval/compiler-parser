First(Pgm)                        = {kwdprog}
Pgm                               = kwdprog       !Vargroup        !Fcndefs         Main

First(Main)                       = {kwdmain}
Main                              = kwdmain       BBlock

First(BBlock)                     = {brace1}
BBlock                            = brace1        !Vargroup        !Stmts           brace2



First(!Vargroup)                  = {kwdvars}
!Vargroup                         = kwdvars       PPvarlist
!Vargroup                         = !eps

First(PPvarlist)                  = {parens1}
PPvarlist                         = parens1       !Varlist         parens2

First(!Varlist)                   = {kint, kfloat, kstring, id, kwdclass}
!Varlist                          = Varitem       semi            !Varlist
!Varlist                          = !eps

First(Varitem)                    = {kint, kfloat, kstring, id} U {kwdclass}
Varitem                           = Vardecl       !Varitem_Suffix
Varitem                           = Classdef

First(!Varitem_Suffix)            = {equal}
!Varitem_Suffix                   = equal           Varinit
!Varitem_Suffix                   = !eps

First(Vardecl)                    = {kint, kfloat, kstring, id}
Vardecl                           = Simplekind    Varspec

First(Simplekind)                 = {kint, kfloat, kstring} U {id}
Simplekind                        = Basekind
Simplekind                        = Classid

First(Basekind)                   = {kint, kfloat, kstring}
Basekind                          = kint
Basekind                          = kfloat
Basekind                          = kstring

First(Classid)                    = {id}
Classid                           = id

First(Varspec)                    = {aster} U {id}
Varspec                           = Varid         !Arrspec
Varspec                           = Deref_id

First(Varid)                      = {id}
Varid                             = id

First(!Arrspec)                   = {bracket1}
!Arrspec                          = KKint
!Arrspec                          = !eps

First(KKint)                      = {bracket1}
KKint                             = bracket1      int             bracket2

First(Deref_id)                   = {aster}
Deref_id                          = Deref         id

First(Deref)                      = {aster}
Deref                             = aster



First(Varinit)                    = {int, float, string, aster, id, ampersand, parens1} U {brace1}
Varinit                           = Expr
Varinit                           = BBexprs

First(BBexprs)                    = {brace1}
BBexprs                           = brace1        !Exprlist        brace2

First(!Exprlist)                  = {int, float, string, aster, id, ampersand, parens1}
!Exprlist                         = Expr          !Moreexprs
!Exprlist                         = !eps

First(!Moreexprs)                 = {comma}
!Moreexprs                        = comma         Expr            !Moreexprs
!Moreexprs                        = !eps



First(Classdef)                   = {kwdclass}
Classdef                          = Classheader   Classdef_Suffix

First(Classdef_Suffix)            = {brace1} U {kif}
Classdef_Suffix                   = BBClassitems
Classdef_Suffix                   = kif             BBClassitems

First(BBClassitems)               = {brace1}
BBClassitems                      = brace1        !Classitems      brace2

First(Classheader)                = {kwdclass}
Classheader                       = kwdclass      Classid     !Classmom        !Interfaces

First(!Classmom)                  = {colon}
!Classmom                         = colon         Classid
!Classmom                         = !eps

First(!Classitems)                = {colon, kwdvars, kwdfcn}
!Classitems                       = !Classgroup    !Classitems
!Classitems                       = !eps

First(!Classgroup)                = {colon} U {kwdvars} U {kwdfcn}
!Classgroup                       = Class_ctrl
!Classgroup                       = !Vargroup
!Classgroup                       = !Mddecls

First(Class_ctrl)                 = {colon}
Class_ctrl                        = colon         id

First(!Interfaces)                = {plus}
!Interfaces                       = plus          Classid         !Interfaces
!Interfaces                       = !eps



First(!Mddecls)                   = {kwdfcn}
!Mddecls                          = Mdheader      !Mddecls
!Mddecls                          = !eps

First(Mdheader)                   = {kwdfcn}
Mdheader                          = kwdfcn        Md_id           PParmlist       Retkind

First(Md_id)                      = {id}
Md_id                             = Classid       colon           Fcnid


First(!Fcndefs)                   = {kwdfcn}
!Fcndefs                          = Fcndef        !Fcndefs
!Fcndefs                          = !eps

First(Fcndef)                     = {kwdfcn}
Fcndef                            = Fcnheader     BBlock

First(Fcnheader)                  = {kwdfcn}
Fcnheader                         = kwdfcn        Fcnid           PParmlist       Retkind

First(Fcnid)                      = {id}
Fcnid                             = id

First(Retkind)                    = {kint, kfloat, kstring}
Retkind                           = Basekind

First(PParmlist)                  = {parens1}
PParmlist                         = parens1       !Varspecs        parens2

First(!Varspecs)                  = {aster, id}
!Varspecs                         = Varspec       !More_varspecs
!Varspecs                         = !eps

First(!More_varspecs)             = {comma}
!More_varspecs                    = comma         Varspec         !More_varspecs
!More_varspecs                    = !eps



First(!Stmts)                     = {aster, id, kwdif, kwdwhile, kprint, kwdreturn}
!Stmts                            = Stmt            semi            !Stmts
!Stmts                            = !eps

First(Stmt)                       = {aster, id} U {kwdif} U {kwdwhile} U {kprint} U {kwdreturn}
Stmt                              = StasgnOrFcall
Stmt                              = Stif
Stmt                              = Stwhile
Stmt                              = Stprint
Stmt                              = Strtn

First(StasgnOrFcall)              = {aster} U {id}
StasgnOrFcall                     = Deref_id        Stasgn_Suffix                     // It's  Stasgn
StasgnOrFcall                     = id              StasgnOrFcall_Suffix              // It's  Either one

First(StasgnOrFcall_Suffix)       = {bracket1, equal} U {parens1}
StasgnOrFcall_Suffix              = !Lval_Suffix    Stasgn_Suffix                     // It's  Stasgn
StasgnOrFcall_Suffix              = PPexprs                                           // It's  Fcall

First(Stasgn_Suffix)              = {equal}
Stasgn_Suffix                     = equal           Expr

First(!Lval_Suffix)               = {bracket1}
!Lval_Suffix                      = KKexpr
!Lval_Suffix                      = !eps

First(KKexpr)                     = {bracket1}
KKexpr                            = bracket1        Expr            bracket2


First(Fcall)                      = {id}
Fcall                             = Fcnid           PPexprs

First(PPexprs)                    = {parens1}
PPexprs                           = parens1         !Exprlist        parens2


First(Stif)                       = {kwdif}
Stif                              = kwdif           PPexpr          BBlock          !Elsepart

First(!Elsepart)                  = {kwdelseif, kwdelse}
!Elsepart                         = kwdelseif       PPexpr          BBlock          !Elsepart
!Elsepart                         = kwdelse         BBlock
!Elsepart                         = !eps


First(Stwhile)                    = {kwdwhile}
Stwhile                           = kwdwhile        PPexpr          BBlock

First(Stprint)                    = {kprint}
Stprint                           = kprint          PPexprs


First(Strtn)                      = {kwdreturn}
Strtn                             = kwdreturn       !Strtn_Suffix

First(!Strtn_Suffix)              = {int, float, string, aster, id, ampersand, parens1}
!Strtn_Suffix                     = Expr
!Strtn_Suffix                     = !eps


First(PPexpr)                     = {parens1}
PPexpr                            = parens1         Expr            parens2

First(Expr)                       = {int, float, string, aster, id, ampersand, parens1}
Expr                              = Rterm           !Expr_Tail

First(!Expr_Tail)                 = {opeq, opne, angle1, ople, opge, angle2}
!Expr_Tail                        = Oprel           Rterm         !Expr_Tail
!Expr_Tail                        = !eps

First(Rterm)                      = {int, float, string, aster, id, ampersand, parens1}
Rterm                             = Term            !Rterm_Tail

First(!Rterm_Tail)                = {plus, minus}
!Rterm_Tail                       = Opadd           Term          !Rterm_Tail
!Rterm_Tail                       = !eps

First(Term)                       = {int, float, string, aster, id, ampersand, parens1}
Term                              = Fact            !Term_Tail

First(!Term_Tail)                 = {aster, slash, caret}
!Term_Tail                        = Opmul           Fact          !Term_Tail
!Term_Tail                        = !eps

First(Fact)                       = {int, float, string} U {aster, id} U {ampersand} U {parens1}
Fact                              = BaseLiteral
Fact                              = LvalOrFcall
Fact                              = Addrof_id
Fact                              = PPexpr

First(LvalOrFcall)                = {aster} U {id}
LvalOrFcall                       = Deref_id                      // Is Lval
LvalOrFcall                       = id         !LvalOrFcall_Suffix // Either one

First(!LvalOrFcall_Suffix)        = {bracket1} U {parens1}
!LvalOrFcall_Suffix               = !Lval_Suffix                   // It's an Lval
!LvalOrFcall_Suffix               = PPexprs                       // It's a Fcall

First(BaseLiteral)                = {int, float, string}
BaseLiteral                       = int
BaseLiteral                       = float
BaseLiteral                       = string

First(Addrof_id)                  = {ampersand}
Addrof_id                         = ampersand       id

First(Oprel)                      = {opeq, opne, angle1, ople, opge, angle2}
Oprel                             = opeq
Oprel                             = opne
Oprel                             = angle1
Oprel                             = ople
Oprel                             = opge
Oprel                             = angle2

First(Opadd)                      = {plus, minus}
Opadd                             = plus
Opadd                             = minus

First(Opmul)                      = {aster, slash, caret}
Opmul                             = aster
Opmul                             = slash
Opmul                             = caret
